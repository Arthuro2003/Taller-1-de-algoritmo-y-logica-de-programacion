Funcion resultado = letrashex(cociente)
	Definir resultado Como Caracter
	Segun cociente  Hacer
		10:
			resultado <- 'A'
		11:
			resultado <- 'B'
		12:
			resultado <- 'C'
		13:
			resultado <- 'D'
		14:
			resultado <- 'E'
		15:
			resultado <- 'F'
	FinSegun
FinFuncion

Funcion resultado = conversordesdedecimal(num,base)
	Definir resultado,digito Como Caracter
	Definir cociente Como Entero
	resultado <- ''; digito <- ''; cociente <- 0
	Si num=0 Entonces
		resultado <- '0'
	FinSi
	Mientras num>0 Hacer
		cociente <- num MOD base
		digito <- ConvertirATexto(cociente)
		Si cociente>9 Entonces
			digito <- letrashex(cociente)
		FinSi
		resultado <- Concatenar(digito,resultado)
		num <- trunc(num/base)
	FinMientras
FinFuncion

Algoritmo binario_octal_hexadecimal
	// se definen las variables
	Definir decimal,base Como Entero
	Definir solucion Como Caracter
	Definir r,b,oc,h Como Caracter
	decimal <- 0; solucion <- ''; base <- 0; b <- 'b'; oc <- 'o'; h <- 'h'
	// entrada 
	Escribir 'dime un numero '
	Leer decimal
	Escribir 'covertir a base ?'
	Escribir 'binario (B)'
	Escribir 'octal (O)'
	Escribir 'hexadecimal(H)'
	Leer r
	Si r=b Entonces
		base <- 2
	SiNo
		Si r=oc Entonces
			base <- 8
		SiNo
			base <- 16
		FinSi
	FinSi
	Escribir conversordesdedecimal(decimal,base)
FinAlgoritmo
